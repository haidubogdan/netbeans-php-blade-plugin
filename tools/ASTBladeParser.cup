/* Minijava Grammar */
package org.netbeans.modules.php.blade.editor.parsing;

import java.util.*;
import org.netbeans.modules.csl.api.OffsetRange;
import org.openide.util.Pair;
import org.netbeans.modules.php.blade.editor.parsing.astnodes.*;
import org.netbeans.modules.php.editor.parser.ParserErrorHandler;
import org.netbeans.modules.php.editor.parser.astnodes.Program;

parser code {:

    private ErrorStrategy defaultStrategy = new DefaultErrorStrategy();;
    private ErrorStrategy errorStrategy = defaultStrategy;

    private String fileName = null;
    private ParserErrorHandler errorHandler = null;

    public void setErrorHandler (ParserErrorHandler handler) {
        this.errorHandler = handler;
    }

    public ParserErrorHandler getErrorHandler () {
        return this.errorHandler;
    }

    public String getFileName() {
        return fileName;
    }

    public void setFileName(String fileName) {
        assert fileName != null;
        this.fileName = fileName;
    }


    interface ErrorStrategy {
        public boolean errorRecovery(boolean debug) throws Exception;
    }

    class DefaultErrorStrategy implements ErrorStrategy {

        public boolean errorRecovery(boolean debug) throws Exception {
            return ASTBladeParser.super.error_recovery(debug);
        }
    }

    /**
     * Attempt to recover from a syntax error.  This returns false if recovery fails,
     * true if it succeeds.
     * @param debug should we produce debugging messages as we parse.
     */
    protected boolean error_recovery(boolean debug) throws java.lang.Exception {
        return errorStrategy.errorRecovery(debug);
    }

    /**
     * Report a non fatal error (or warning).  This method takes a message
     * string and an additional object (to be used by specializations implemented in subclasses).
     * The super class prints the message to System.err.
     * @param message an error message.
     * @param info    an extra object reserved for use by specialized subclasses.
     */
    public void report_error(String message, Object info) {
        System.out.print("report_eror"  + message);
    }

    /**
     * This method is called when a syntax error has been detected and recovery is about to be invoked.
     * The super class just emit a "Syntax error" error message.
     * @param cur_token the current lookahead Symbol.
     */
    public void syntax_error(java_cup.runtime.Symbol cur_token) {
        java_cup.runtime.Symbol symbol = (java_cup.runtime.Symbol)stack.peek();
        int state = symbol.parse_state;
        short[] rowOfProbe = action_tab[state];
        if (errorHandler != null) {
            errorHandler.handleError(ParserErrorHandler.Type.SYNTAX_ERROR, rowOfProbe, cur_token, symbol);
        }
     }

    /**
     * Report a fatal error.  This method takes a message string and an additional object
     * (to be used by specializations implemented in subclasses).
     * The super class reports the error then throws an exception.
     * @param message an error message.
     * @param info    an extra object reserved for use by specialized subclasses.
     */
    public void report_fatal_error(String message, Object info) throws Exception {
        if (errorHandler != null) {
            errorHandler.handleError(ParserErrorHandler.Type.FATAL_PARSER_ERROR, null, cur_token, null);
        }
    }

    protected int error_sync_size() {
        return 1;
    }
:}; 

terminal String T_INLINE_HTML, T_INLINE_PHP, T_BLADE_INLINE_PHP;
terminal String T_BLADE_COMMENT;
terminal String T_BLADE_OPEN_ECHO, T_BLADE_CLOSE_ECHO;
terminal String T_BLADE_INCLUDE;
terminal String T_BLADE_INCLUDE_FIRST, T_BLADE_INCLUDE_CONDITIONAL;
terminal String T_PARAMETER_EXPRESSION;
terminal String T_BLADE_SECTION;
terminal String T_BLADE_ENDSECTION, T_BLADE_STOP, T_BLADE_OVERWRITE, T_BLADE_APPEND;
terminal String T_BLADE_SHOW;
terminal String T_BLADE_EXTENDS;
terminal String T_BLADE_YIELD;
terminal String T_BLADE_EACH;

terminal String T_BLADE_FOREACH;
terminal String T_BLADE_ENDFOREACH;
terminal String T_BLADE_FOR;
terminal String T_BLADE_ENDFOR;
terminal String T_BLADE_IF, T_BLADE_CONDITION_OPEN_TAG;
terminal String T_BLADE_ELSEIF;
terminal String T_BLADE_ELSEIF_END; //pseudo symbol to mark a else if end
terminal String T_BLADE_ENDIF;
terminal String T_BLADE_SWITCH, T_BLADE_ENDSWITCH;
terminal String T_BLADE_DIRECTIVE;
terminal String T_PHP_LOOP_EXPRESSION, T_BLADE_PHP_ECHO;
terminal String T_PHP_CONDITION_EXPRESSION;
terminal String T_PHP_PARAMETER_EXPRESSION;

terminal String T_STRING;
terminal String T_VARIABLE;

terminal T_COMMA;
terminal T_NEKUDA;
terminal T_OPEN_PARENTHESE,T_CLOSE_PARENTHESE;


/* Non terminals */
non terminal end_section;
non terminal BladeProgram thestart;
non terminal List top_statement_list;
non terminal List variableList;
non terminal Statement top_statement;
non terminal Statement statement;
non terminal List inner_statement_list;
non terminal ArgumentExpression path_expression;
non terminal Expression path;
non terminal ArgumentExpression yield_label_expression;
non terminal Expression yield_label;
non terminal Variable variable;
non terminal section_param;
non terminal possible_include_render_params;

non terminal Statement directive_statement;
non terminal directive_identifier;

non terminal Statement d_section_statement;
non terminal d_if_statement;

precedence left T_BLADE_INCLUDE, T_BLADE_SECTION, T_BLADE_EXTENDS, T_BLADE_FOREACH, T_BLADE_FOR, T_BLADE_IF, T_BLADE_ELSEIF, T_BLADE_CONDITION_OPEN_TAG;
precedence left T_INLINE_HTML, T_INLINE_PHP, T_BLADE_OPEN_ECHO;
precedence left T_COMMA;
precedence left T_NEKUDA;
precedence right T_PHP_LOOP_EXPRESSION, T_PHP_CONDITION_EXPRESSION, T_PHP_PARAMETER_EXPRESSION;


thestart ::=
top_statement_list:statementList
{:
    ASTBladeScanner bladeAstLexer = (ASTBladeScanner) parser.getScanner();
    int endOfProgram = statementListright > bladeAstLexer.getWhitespaceEndPosition() ? statementListright : bladeAstLexer.getWhitespaceEndPosition();
    BladeProgram program = new BladeProgram(statementListleft, endOfProgram, statementList, bladeAstLexer.getPhpParserResult());
    RESULT = program;
:}
;

top_statement_list ::=
top_statement_list:sList top_statement:statement
{:
    //check for extends
    if(statement != null) {
        sList.add(statement);
    }
    RESULT = sList;
:}

| /* empty */
{:
    RESULT = new LinkedList();
:}
;

inner_statement_list ::=
inner_statement_list:statementList top_statement:statement
{:
    // Ignore null statements
    if(statement != null) {
        statementList.add(statement);
    }
    RESULT = statementList;
:}

| /* empty */
{:
    RESULT = new LinkedList();
:}
;

top_statement ::=
statement:statement
{:
    RESULT = statement;
:}
;

statement ::=
directive_statement:stm {:
	RESULT = stm;
:}
| T_BLADE_EXTENDS:exts path_expression:path {:
    //for the moment until we find to make use of <<EOL>>
    DirectiveName directive = new DirectiveName(extsleft, extsright, exts.toString());
    RESULT = new BladeExtendsStatement(extsleft, pathright, directive, path);
:}
| T_BLADE_EXTENDS:exts T_OPEN_PARENTHESE:po variable:path T_COMMA T_PHP_PARAMETER_EXPRESSION:p T_CLOSE_PARENTHESE:end {:
    //for the moment until we find to make use of <<EOL>>
    PhpExpression parameter = new PhpExpression(pleft, pright, p.toString());
    DirectiveName directive = new DirectiveName(extsleft, extsright, exts.toString());
    ArgumentExpression expr = new ArgumentExpression(path, poleft, endright, parameter);
    RESULT = new BladeExtendsStatement(extsleft, endright, directive, expr);
:}
| T_BLADE_YIELD:d T_OPEN_PARENTHESE:po yield_label:s T_CLOSE_PARENTHESE:end {:
    String strPath = "";
    if (s != null){
        strPath = s.toString();
    }
    //to be refactored to handle parameter directly

    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    SectionName label = new SectionName(sleft, sright, strPath);
    ArgumentExpression expr = new ArgumentExpression(label, poleft, endright);
    RESULT = new BladeYieldStatement(dleft, endright, directive, expr);
:}
| T_BLADE_YIELD:d T_OPEN_PARENTHESE:po yield_label:s T_COMMA T_PHP_PARAMETER_EXPRESSION:p T_CLOSE_PARENTHESE:end {:
    String strPath = "";
    if (s != null){
        strPath = s.toString();
    }
    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    SectionName label = new SectionName(sleft, sright, strPath);
    ArgumentExpression expr = new ArgumentExpression(label, poleft, endright);
    RESULT = new BladeYieldStatement(dleft, endright, directive, expr);
:}
| T_BLADE_INCLUDE:d T_OPEN_PARENTHESE:po variableList:varList T_CLOSE_PARENTHESE:end {:
	String strPath = "";
    
    Object v;
    for (int i=0 ; i < varList.size() ; i++) {
        
        v = varList.get(i);
        if (v instanceof String){
            //stop at first string
            strPath = (String) v;
            break;
        } else if ((v instanceof Variable) && !v.toString().startsWith("$")){
            strPath = v.toString();
            break;
        }
    }

    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    PathExpression label = new PathExpression(varListleft, varListright, strPath);
    ArgumentExpression expr = new ArgumentExpression(label, poleft, endright);
    RESULT = new BladeIncludeStatement(dleft, endright, directive, expr);
:}
| T_BLADE_INCLUDE:d T_OPEN_PARENTHESE:po variableList:varList T_COMMA T_PHP_PARAMETER_EXPRESSION:p T_CLOSE_PARENTHESE:end {:
    String strPath = "";
    String strV = "";
    Object v;
    for (int i=0 ; i < varList.size() ; i++) {
        
        v = varList.get(i);
        if (v instanceof String){
            //stop at first string
            strPath = (String) v;
            break;
        } else if ((v instanceof Variable) && !v.toString().startsWith("$")){
            strPath = v.toString();
            break;
        }
    }
    PathExpression label = new PathExpression(varListleft, varListright, strPath);
    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    Variable parameter = new Variable(0, 1, strV);
    ArgumentExpression expr = new ArgumentExpression(label, poleft, endright, parameter);
    RESULT = new BladeIncludeStatement(dleft, endright, directive, expr);
:}
| T_BLADE_INCLUDE_CONDITIONAL:d T_OPEN_PARENTHESE:po T_PARAMETER_EXPRESSION:b T_COMMA  T_PARAMETER_EXPRESSION:path_list possible_include_render_params:param T_CLOSE_PARENTHESE:end {:
    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    PathExpression label = new PathExpression(path_listleft, path_listright, path_list.toString());
    Variable parameter = new Variable(0, 1, "");
    ArgumentExpression expr = new ArgumentExpression(label, poleft, endright, parameter);
    RESULT = new BladeConditionalIncludeStatement(dleft, endright, directive, expr);
:}
| T_BLADE_INCLUDE_FIRST:d T_OPEN_PARENTHESE:po T_PARAMETER_EXPRESSION:path_list possible_include_render_params:param T_CLOSE_PARENTHESE:end {:
    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    PathExpression label = new PathExpression(path_listleft, path_listright, path_list.toString());
    Variable parameter = new Variable(0, 1, "");
    ArgumentExpression expr = new ArgumentExpression(label, poleft, endright, parameter);
    RESULT = new BladeIncludeFirstStatement(dleft, endright, directive, expr);
:}
| T_BLADE_EACH:d T_OPEN_PARENTHESE:po variableList:varList T_COMMA T_PHP_PARAMETER_EXPRESSION:p T_CLOSE_PARENTHESE:end {:
    String strPath = "";
    String strV = "";
    Object v;
    for (int i=0 ; i < varList.size() ; i++) {
        
        v = varList.get(i);
        if (v instanceof String){
            //stop at first string
            strPath = (String) v;
            break;
        } else if ((v instanceof Variable) && !v.toString().startsWith("$")){
            strPath = v.toString();
            break;
        }
    }
    PathExpression label = new PathExpression(varListleft, varListright, strPath);
    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    Variable parameter = new Variable(0, 1, strV);
    ArgumentExpression expr = new ArgumentExpression(label, poleft, endright, parameter);
    RESULT = new BladeEachStatement(dleft, endright, directive, expr);
:}
| T_BLADE_FOREACH:d T_PHP_LOOP_EXPRESSION:s inner_statement_list:statementList T_BLADE_ENDFOREACH:end {:
    Block block = new Block(statementListleft, statementListright, statementList);
    PhpExpression loopExpr = new PhpExpression(sleft, sright, s);
    DirectiveName directive = new DirectiveName(dleft, dright, d);
    ArgumentExpression expr = new ArgumentExpression(loopExpr, sleft, sright);
    DirectiveEndTag endTag = new DirectiveEndTag(endleft, endright, end.toString());
    RESULT = new BladeForeachStatement(dleft, endright, directive, expr, block, endTag);
:}
| T_BLADE_FOR:d T_PHP_LOOP_EXPRESSION:s inner_statement_list:statementList T_BLADE_ENDFOR:end {:
    Block block = new Block(statementListleft, statementListright, statementList);
    PhpExpression loopExpr = new PhpExpression(sleft, sright, s);
    DirectiveName directive = new DirectiveName(dleft, dright, d);
    ArgumentExpression expr = new ArgumentExpression(loopExpr, sleft, sright);
    DirectiveEndTag endTag = new DirectiveEndTag(endleft, endright, end.toString());
    RESULT = new BladeForStatement(dleft, endright, directive, expr, block, endTag);
:}
| T_BLADE_IF:d T_PHP_CONDITION_EXPRESSION:s inner_statement_list:statementList T_BLADE_ENDIF:end {:
    Block block = new Block(statementListleft, statementListright, statementList);
    PhpExpression ifExpr = new PhpExpression(sleft, sright, s.toString());
    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    ArgumentExpression expr = new ArgumentExpression(ifExpr, sleft, sright);
    DirectiveEndTag endTag = new DirectiveEndTag(endleft, endright, end.toString());
    RESULT = new BladeIfStatement(dleft, endright, directive, expr, block, endTag);
:}
| T_BLADE_ELSEIF:d T_PHP_CONDITION_EXPRESSION:s inner_statement_list:statementList T_BLADE_ELSEIF_END:end {:
    Block block = new Block(statementListleft, statementListright, statementList);
    PhpExpression ifExpr = new PhpExpression(sleft, sright, s.toString());
    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    ArgumentExpression expr = new ArgumentExpression(ifExpr, sleft, sright);
    DirectiveEndTag endTag = new DirectiveEndTag(endleft, endright, "elseifend");
    RESULT = new BladeIfStatement(dleft, endright, directive, expr, block, endTag);
:}
| T_BLADE_CONDITION_OPEN_TAG:d T_PHP_CONDITION_EXPRESSION:expr inner_statement_list:statementList T_BLADE_ENDIF:end {:
    Block block = new Block(statementListleft, statementListright, statementList);
    PhpExpression phpExpression = new PhpExpression(exprleft, exprright, expr.toString());
    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    ArgumentExpression argexpr = new ArgumentExpression(phpExpression, exprleft, exprright);
    DirectiveEndTag endTag = new DirectiveEndTag(endleft, endright, end.toString());
    RESULT = new BladeConditionStatement(dleft, endright, directive, argexpr, block, endTag);
:}
| T_BLADE_CONDITION_OPEN_TAG:d T_PHP_CONDITION_EXPRESSION:expr inner_statement_list:statementList T_BLADE_ELSEIF_END T_BLADE_ENDIF:end {:
    Block block = new Block(statementListleft, statementListright, statementList);
    PhpExpression phpExpression = new PhpExpression(exprleft, exprright, expr.toString());
    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    ArgumentExpression argexpr = new ArgumentExpression(phpExpression, exprleft, exprright);
    DirectiveEndTag endTag = new DirectiveEndTag(endleft, endright, end.toString());
    RESULT = new BladeConditionStatement(dleft, endright, directive, argexpr, block, endTag);
:}
| T_BLADE_SWITCH:d T_OPEN_PARENTHESE:po T_PHP_PARAMETER_EXPRESSION:expr T_CLOSE_PARENTHESE:pc inner_statement_list:statementList T_BLADE_ENDSWITCH:end {:
    Block block = new Block(statementListleft, statementListright, statementList);
    PhpExpression phpExpression = new PhpExpression(exprleft, exprright, expr.toString());
    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    ArgumentExpression argexpr = new ArgumentExpression(phpExpression, poleft, pcright);
    DirectiveEndTag endTag = new DirectiveEndTag(endleft, endright, end.toString());
    RESULT = new BladeSwitchStatement(dleft, endright, directive, argexpr, block, endTag);
:}
|  T_BLADE_DIRECTIVE:d T_OPEN_PARENTHESE T_PHP_PARAMETER_EXPRESSION:expr T_CLOSE_PARENTHESE {:
    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    PhpExpression phpExpression = new PhpExpression(exprleft, exprright, expr.toString());
    ArgumentExpression argexpr = new ArgumentExpression(phpExpression, exprleft, exprright);
    RESULT = new DirectiveWithArgument(dleft, exprright, directive, argexpr);
:}
| T_BLADE_DIRECTIVE:d {:
    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    RESULT = new BladeConstDirectiveStatement(dleft, dright, directive);
:}
| T_BLADE_OPEN_ECHO:token T_BLADE_PHP_ECHO:php T_BLADE_CLOSE_ECHO:end {:
    Expression expr = new PhpExpression(phpleft, phpright, php);
    RESULT = new BladeEchoStatement(tokenleft, endright, token, expr);
:}
| T_INLINE_HTML:html {:
    InLineHtml inLineHtml = new InLineHtml(htmlleft, htmlright, html);
    RESULT = inLineHtml;
:}
| T_INLINE_PHP:php {:
    InLinePhp inLinePhp = new InLinePhp(phpleft, phpright, php);
    RESULT = inLinePhp;
:}
| T_BLADE_INLINE_PHP:php {:
    InLineBladePhp inLinePhp = new InLineBladePhp(phpleft, phpright, php);
    RESULT = inLinePhp;
:}
| T_STRING:html {:
    //unkown string
    InLineHtml inLineHtml = new InLineHtml(htmlleft, htmlright, html);
    RESULT = inLineHtml;
:}
| T_NEKUDA:html {:
	//stray element
    InLineHtml inLineHtml = new InLineHtml(htmlleft, htmlright, ":");
    RESULT = inLineHtml;
:}
| T_BLADE_COMMENT:comment {:
    /* comment */
    BladeComment commentSt = new BladeComment(commentleft, commentright, comment);
    RESULT = commentSt;
:}

| error:theError /* error statement */
{:
    ASTError error = new ASTError(theErrorleft, theErrorright);
    RESULT = error;
:}
;


directive_statement ::= 
d_section_statement:stm{:
	RESULT = stm;
:};

d_section_statement ::= 
T_BLADE_SECTION:d T_OPEN_PARENTHESE:po yield_label:label T_COMMA T_PHP_PARAMETER_EXPRESSION:p T_CLOSE_PARENTHESE:end {:
    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    PhpExpression phpExpression = new PhpExpression(pleft, pright,  p.toString());
    ArgumentExpression argexpr = new ArgumentExpression(phpExpression, poleft, endright, phpExpression);
    RESULT = new BladeInlineSectionStatement(dleft, endright, directive, argexpr);
:}
| T_BLADE_SECTION:d yield_label_expression:label inner_statement_list:statementList end_section:end{:
    Block block = new Block(statementListleft, statementListright, statementList);
    DirectiveName directive = new DirectiveName(dleft, dright, d.toString());
    DirectiveEndTag endTag = new DirectiveEndTag(endleft, endright, end.toString());
    RESULT = new BladeSectionStatement(dleft, endright, directive, label, block, endTag);
:};

possible_include_render_params::=
T_COMMA T_PARAMETER_EXPRESSION:param {:
    //todo add 
:}
| /* empty */ {:

:}
;

end_section ::=
T_BLADE_ENDSECTION:token{: 
    RESULT = token;
:}
| T_BLADE_SHOW:token {:
    RESULT = token;
:}
| T_BLADE_STOP:token {:
    RESULT = token;
:}
| T_BLADE_APPEND:token {: 
   RESULT = token;
:}
| T_BLADE_OVERWRITE:token {: RESULT = token; :}
;

yield_label_expression ::=
T_OPEN_PARENTHESE:po yield_label:label T_CLOSE_PARENTHESE:pc {:
    RESULT =  new ArgumentExpression(label, poleft, pcright);;
:}
;

yield_label ::=
T_STRING:label {:
    if (label == null) {
        label = "";
    } else if (label.length() > 2) {
    	//remove quotes
    	label = label.substring(1, label.length() -1);
    }
	RESULT = new SectionName(labelleft, labelright, label.toString());
:}
| T_PHP_PARAMETER_EXPRESSION:label {:
	if (label == null) {
        label = "";
    }
	RESULT = new SectionName(labelleft, labelright, label.toString());
:}
;

variable ::=
T_VARIABLE:var {:
    if (var == null) {
        var = "";
    }
    RESULT = new Variable(varleft, varright, var.toString());
:}
| T_STRING:var {:
    if (var == null) {
        var = "";
    }
    else if (var.length() > 2) {
    	//remove quotes
    	var = var.substring(1, var.length() -1);
    }
    RESULT = new Variable(varleft, varright, var.toString());    
:}
| T_PHP_PARAMETER_EXPRESSION:var {: RESULT = new Variable(varleft, varright, var.toString());  :}
| variable:expr1 T_NEKUDA variable:expr2 {:
    //we will force just one string
    String expr1Str = "";
                if (expr1 != null){
                    expr1Str = expr1.toString();
                }
    RESULT = new Variable(expr1left, expr1right, expr1Str);
:}
;

variableList ::=
variableList:statementList variable:var 
{:
    // Ignore null statements
    if(var != null) {
        statementList.add(var);
    }
    RESULT = statementList;
:}
| /* empty */
{:
    RESULT = new LinkedList();
:};

path_expression ::=
T_OPEN_PARENTHESE:o path:path T_CLOSE_PARENTHESE:e {:
	RESULT = new ArgumentExpression(path, oleft, eright );
:}
;

path ::=
T_STRING:path {:
    if (path == null) {
        path = "";
    }
    else if (path.length() > 2) {
    	//remove quotes
    	path = path.substring(1, path.length() -1);
    }
	RESULT = new PathExpression(pathleft, pathright, path);
:}
| T_PHP_PARAMETER_EXPRESSION:path {:
    if (path == null) {
        path = "";
    }
	RESULT = new PathExpression(pathleft, pathright, path.toString());
:}
| error:expr
{:
    RESULT = new ASTErrorExpression(exprleft, exprright);
:}
;
